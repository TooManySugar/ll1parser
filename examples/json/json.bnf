<json> ::= <element>
<element> ::= <opt-whitespaced-value>
<opt-value-list> ::=  "" | <opt-whitespaced-value> <opt-value-list-tail>
<opt-value-list-tail> ::= "" | "," <opt-whitespaced-value> <opt-value-list-tail>
<opt-key-value-list> ::= "" | <key-value> <opt-whitespace> <opt-key-value-list-tail>
<opt-key-value-list-tail> ::= "" | "," <opt-whitespace> <key-value> <opt-whitespace> <opt-key-value-list-tail>
<key-value> ::= <string> <opt-whitespace> ":" <opt-whitespace> <value>
<opt-whitespaced-value> ::= <opt-whitespace> <value> <opt-whitespace>
<value> ::= <object> | <array> | <string> | <number> | "true" | "false" | "null"
<object> ::= "{" <opt-whitespace> <opt-key-value-list> "}"
<array> ::= "[" <opt-value-list> "]"
<string> ::= '"' <text> '"'
<text> ::= "" | <character> <text>
<character> ::= <digit> | <letter> | <escape-sequence> | <symbol>
<escape-sequence> ::= '\' <escape>
<escape> ::= '"' | '\' | '/' | 'b' | 'f' | 'n' | 'r' | 't' | 'u' <hex> <hex> <hex>
<hex> ::= <digit> | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b" | "c" | "d" | "e" | "f"
<number> ::= <integer> <fraction> <exponent>
<integer> ::= <opt-minus> <unsigned-integer>
<unsigned-integer> ::= "0" | <one-nine> <opt-digits>
<opt-minus> ::= "" | "-"
<fraction> ::= "" | "." <digits>
<exponent> ::= "" | "E" <opt-sign> <digits> | "e" <opt-sign> <digits>
<digits> ::= <digit> <opt-digits>
<opt-digits> ::= "" | <digit> <opt-digits>
<opt-sign> ::= "" | "+" | "-"
<digit> ::= "0" | <one-nine>
<one-nine> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<letter> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<symbol> ::= "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "]" | "^" | "_" | "`" | "{" | "}" | "~" | "'"
<opt-whitespace> ::= "" | <space-symobls> <opt-whitespace>
<space-symobls> ::= " " | <EOL>
